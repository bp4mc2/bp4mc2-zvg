###################
# AANPAK
###################

1. Uploaden XMI
2. Voer de queries uit m.b.t. het opschonen van het UML model
3. Uitvoeren: "Create MIM model from UML"
4. Voer de queries uit m.b.t. het opschonen van het MIM model
5. Ga naar "Edit MIM model", fix prefixes en sla deze op als mim/model-mim-ld.ttl
6. Uitvoeren: "Create model from MIM"
7. Voer de queries uit m.b.t. het opschonen van het model (dwz: de ontologie)
8. Ga naar "Edit model", fix prefixes en sla deze op als rdf/ontologie.ttl
9. Ga naar Download, Grafische weergave (technische namen) en sla yEd plaatje op als rdf/ontologie.graphml en rdf/ontologie.png
10. Herlaad het model, voer de queries uit voor de shapesgraph en query verwijderen string datatype (onder Model opschonen)
11. Ga naar "Edit model", fix prefixes en sla deze op als shapesgraph.ttl
12. Herlaad het model, voer de queries uit voor de owl-rdfs en query verwijderen string datatype (onder Model opschonen)
13. Ga naar "Edit model", fix prefixes en sla deze op als owl-rdfs-ontologie.ttl

###################
# UML Model opschonen
###################

# Wijzig 1 specifiek datatype van klasse uml:Class naar uml:DataType
PREFIX uml: <http://schema.omg.org/spec/UML/2.1.1#>
DELETE {
  GRAPH <http://localhost:8080/data2model/container/data> {
    <uuid:EAID_BB22F120_C7B2_4c67_B59C_134BBC6C9486> a uml:Class.
  }
}
INSERT {
  GRAPH <http://localhost:8080/data2model/container/data> {
    <uuid:EAID_BB22F120_C7B2_4c67_B59C_134BBC6C9486> a uml:DataType.
  }
}
WHERE {
  GRAPH <http://localhost:8080/data2model/container/data> {
    <uuid:EAID_BB22F120_C7B2_4c67_B59C_134BBC6C9486> a uml:Class.
  }
}

# Wijzig "interface" stereotype in "Primitief datatype" stereotype
#
PREFIX uml: <http://schema.omg.org/spec/UML/2.1.1#>
PREFIX ea: <http://www.sparxsystems.com/extender/EA6.5#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
DELETE {
  GRAPH <http://localhost:8080/data2model/container/data> {
    ?class ea:stereotype ?stereotype.
  }
}
INSERT {
  GRAPH <http://localhost:8080/data2model/container/data> {
    ?class ea:stereotype "Primitief datatype"^^xsd:string.
  }
}
WHERE {
  GRAPH <http://localhost:8080/data2model/container/data> {
    ?class a uml:DataType.
    ?class ea:stereotype ?stereotype.
    FILTER REGEX(?stereotype,"^interface$","i")
  }
}

###################
# Model opschonen
###################

# Omzetten van primitieve datatypen naar standaard xsd datatypen
# Zie ook: https://www.w3.org/TR/rdf11-concepts/#xsd-datatypes
#
#string
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:CharacterString
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype xsd:string
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:CharacterString
  }
}
#boolean
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Boolean
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype xsd:boolean
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Boolean
  }
}
#integer
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Integer
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype xsd:integer
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Integer
  }
}
#date
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Date
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype xsd:date
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:Date
  }
}
#dateTime
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:DateTime
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype xsd:dateTime
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:DateTime
  }
}
#Surface
prefix test: <http://test/def#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix sf: <http://www.opengis.net/ont/sf#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:GM_Surface
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype sf:Surface
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?prop sh:datatype test:GM_Surface
  }
}

# Verwijder verwijzing naar klasse Waardelijst
#
prefix test: <http://test/def#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?s rdfs:subClassOf test:Waardelijst
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s rdfs:subClassOf test:Waardelijst
  }
}
# Verwijder ongebruikte datatypen
#
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?type ?ptype ?otype
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?type a rdf:Datatype.
    ?type ?ptype ?otype.
    FILTER NOT EXISTS {
      ?prop sh:datatype ?type
    }
  }
}

# Verwijder string datatype
#
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?strliteral
  }
}
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?literal
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?strliteral
    FILTER (datatype(?strliteral) = xsd:string)
    BIND (str(?strliteral) as ?literal)
  }
}

# Verwijderen rdfs:seeAlso
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
  	?s rdfs:seeAlso ?uml
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s rdfs:seeAlso ?uml
  }
}

###################
# MIM opschonen
###################

# Verwijder rdfs:seeAlso
#
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
delete {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s rdfs:seeAlso ?r
  }
}
where {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s rdfs:seeAlso ?r
  }
}

# Verwijder Kadaster-specifiek gebruik van opmaak in definities
#
prefix mim: <http://bp4mc2.org/def/mim#>
delete {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:definitie ?def
  }
}
insert {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:definitie ?definitie
  }
}
where {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:definitie ?def
	BIND (replace(replace(replace(replace(replace(?def,"---IMKAD---\n",""),"--Definitie--\n",""),"--Toelichting--(\n.*)*$",""),"^[\n| ]+",""),"[\n| ]+$","") as ?definitie)
  }
}

# Verwijder lege definities
#
prefix mim: <http://bp4mc2.org/def/mim#>
delete {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:definitie ""
  }
}
where {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:definitie ""
  }
}

# Verwijder Kadaster-specifiek gebruik van namen van abstracte klassen
#
prefix mim: <http://bp4mc2.org/def/mim#>
delete {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:naam ?anaam
  }
}
insert {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:naam ?naam
  }
}
where {
  graph <http://localhost:8080/data2model/container/mim> {
    ?s mim:naam ?anaam
    BIND (replace(?anaam,"^_(.*)$","$1") as ?naam)
    FILTER (regex(?anaam,"^_(.*)$"))
  }
}

###################
# shapesgraph maken
###################

# Verwijder owl:DatatypeProperty en owl:ObjectProperty
#
PREFIX owl: <http://www.w3.org/2002/07/owl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
	?s ?p ?o
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?o.
    ?s a ?t
  FILTER (?t = owl:ObjectProperty || ?t = owl:DatatypeProperty)
  }
}

# Verwijder owl:Class typering
#
PREFIX owl: <http://www.w3.org/2002/07/owl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
  	?s a owl:Class.
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s a owl:Class.
  }
}

###################
# owl-rdfs ontologie maken
###################

# Verwijder sh:NodeShape typering
#
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
  	?s a sh:NodeShape.
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s a sh:NodeShape.
  }
}

# Omzetten shacl naar rdfs:domain en rdfs:range
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
insert {
  graph <http://localhost:8080/data2model/container/model> {
    ?property rdfs:domain ?domain.
    ?property rdfs:range ?range.
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s sh:targetClass ?domain.
    ?s sh:property ?ps.
    ?ps sh:path ?property.
    OPTIONAL {?ps sh:class ?range}
  }
}

# Verwijder sh:PropertyShape
#
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?o
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s ?p ?o.
    ?s a sh:PropertyShape
  }
}

# Verwijder shacl elementen uit owl:Class specificatie
prefix sh: <http://www.w3.org/ns/shacl#>
delete {
  graph <http://localhost:8080/data2model/container/model> {
    ?s sh:name ?name.
    ?s sh:targetClass ?class.
    ?s sh:property ?prop.
  }
}
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s sh:name ?name.
    ?s sh:targetClass ?class.
    OPTIONAL {?s sh:property ?prop}
  }
}

###########
# Controles
###########

# Classes uit het voorbeeld (voorbeeld zit in /data)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?t ?tlabel
where {
  graph <http://localhost:8080/data2model/container/data> {
    ?s a ?t
  }
  OPTIONAL {
    graph <http://localhost:8080/data2model/container/model> {
      ?t rdfs:label ?tlabel
    }
  }
}

# Properties uit het voorbeeld (voorbeeld zit in /data)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?t ?p
where {
  graph <http://localhost:8080/data2model/container/data> {
    ?s ?p ?o
    OPTIONAL {?s a ?t}.
    FILTER (?p!=rdf:type)
  }
  FILTER NOT EXISTS {
    graph <http://localhost:8080/data2model/container/model> {
      ?p rdfs:label ?plabel
    }
  }
}

# Link naar de begrippen (begrippen zijn opgenomen in /data)
select distinct ?s ?begrip
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?s dct:subject ?begrip
  }
  FILTER NOT EXISTS {
    graph <http://localhost:8080/data2model/container/data> {
      ?begrip a skos:Concept
    }
  }
}

# Ontbrekende datatypen
PREFIX ea: <http://www.sparxsystems.com/extender/EA6.5#>
PREFIX uml: <http://schema.omg.org/spec/UML/2.1.1#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix mim: <http://bp4mc2.org/def/mim#>
select distinct ?class ?stereotype ?type ?type_label
where {
  graph <http://localhost:8080/data2model/container/data> {
    ?attr uml:type ?type.
    ?type rdfs:label ?type_label.
    ?type a ?class.
    ?type ea:stereotype ?stereotype
  }
  FILTER NOT EXISTS {
    graph <http://localhost:8080/data2model/container/mim> {
      ?datatype rdfs:seeAlso ?type
    }
  }
}

# Datatypen die wel/niet in gebruik zijn
prefix sh: <http://www.w3.org/ns/shacl#>
select ?type (count(?prop) as ?aantal)
where {
  graph <http://localhost:8080/data2model/container/model> {
    ?type a rdf:Datatype.
    OPTIONAL {?prop sh:datatype ?type}
  }
}
group by ?type
