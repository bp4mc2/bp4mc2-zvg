title: MIM conversie
version: 0.1
queries:
  - title: create technical names
    query: >
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      INSERT {
        GRAPH <urn:input> {
          ?item skos:notation ?codefixed
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:naam ?naam
          BIND (replace(?naam,"[^a-zA-Z0-9_-]","_") as ?codefixed)
        }
      }
  - title: convert mim:Objecttype
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?class a owl:Class, sh:NodeShape.
          ?class sh:targetClass ?class.
          ?class rdfs:label ?naam.
          ?class skos:notation ?code.
          ?class rdfs:seeAlso ?objecttype.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?class) ?code ?naam ?objecttype
        WHERE {
          GRAPH <urn:input> {
            ?objecttype a mim:Objecttype.
            ?objecttype mim:naam ?naam.
            ?objecttype skos:notation ?code.
          }
        }
      }
  - title: convert mim:Gegevensgroeptype
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?class a owl:Class, sh:NodeShape.
          ?class sh:targetClass ?class.
          ?class rdfs:label ?naam.
          ?class skos:notation ?code.
          ?class rdfs:seeAlso ?gegevensgroeptype.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?class) ?code ?naam ?gegevensgroeptype
        WHERE {
          GRAPH <urn:input> {
            ?gegevensgroeptype a mim:Gegevensgroeptype.
            ?gegevensgroeptype mim:naam ?naam.
            ?gegevensgroeptype skos:notation ?code.
          }
        }
      }
  - title: convert mim:Relatieklasse
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?class a owl:Class, sh:NodeShape.
          ?class sh:targetClass ?class.
          ?class rdfs:label ?naam.
          ?class skos:notation ?code.
          ?class rdfs:seeAlso ?relatieklasse.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?class) ?code ?naam ?relatieklasse
        WHERE {
          GRAPH <urn:input> {
            ?relatieklasse a mim:Relatieklasse.
            ?relatieklasse mim:naam ?naam.
            ?relatieklasse skos:notation ?code.
          }
        }
      }
  - title: convert mim:Attribuutsoort
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:DatatypeProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?attribuutsoort.
          ?ownershape sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate rdfs:seeAlso ?attribuutsoort
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?ownershape)
                ?code ?naam ?attribuutsoort
        WHERE {
          GRAPH <urn:input> {
            ?owner a ?mimtype.
            ?owner mim:attribuut ?attribuutsoort.
            ?owner skos:notation ?ownercode.
            ?attribuutsoort a mim:Attribuutsoort.
            ?attribuutsoort mim:naam ?naam.
            ?attribuutsoort skos:notation ?code.
            FILTER (?mimtype = mim:Objecttype || ?mimtype = mim:Gegevensgroeptype || ?mimtype = mim:Relatieklasse)
          }
        }
      }
  - title: convert mim:Relatiesoort
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatiesoort.
          ?owner sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate sh:class ?class.
          ?predicate rdfs:seeAlso ?relatiesoort
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?owner)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?classcode)) as ?class)
                ?code ?naam ?relatiesoort
        WHERE {
          GRAPH <urn:input> {
            ?relatiesoort a mim:Relatiesoort.
            ?relatiesoort mim:naam ?naam.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?relatiesoort skos:notation ?code.
            ?bron skos:notation ?ownercode.
            ?doel skos:notation ?classcode.
            FILTER NOT EXISTS {
              ?relatiesoort mim:relatierol ?relatieroldoel.
              ?relatieroldoel a mim:RelatierolDoel.
              ?relatieroldoel mim:naam ?relatieroldoelnaam
            }
          }
        }
      }
  - title: convert mim:ExterneKoppeling
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?externekoppeling.
          ?owner sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate sh:class ?class.
          ?predicate rdfs:seeAlso ?externekoppeling
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?owner)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?classcode)) as ?class)
                ?code ?naam ?externekoppeling
        WHERE {
          GRAPH <urn:input> {
            ?externekoppeling a mim:ExterneKoppeling.
            ?externekoppeling mim:naam ?naam.
            ?externekoppeling mim:bron ?bron.
            ?externekoppeling mim:doel ?doel.
            ?externekoppeling skos:notation ?code.
            ?bron skos:notation ?ownercode.
            ?doel skos:notation ?classcode.
          }
        }
      }
  - title: convert mim:PrimitiefDatatype
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?datatype a rdf:Datatype.
          ?datatype rdfs:label ?naam.
          ?datatype rdfs:seeAlso ?type.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?datatype) ?naam ?type
        WHERE {
          GRAPH <urn:input> {
            ?type a mim:PrimitiefDatatype.
            ?type mim:naam ?naam.
            ?type skos:notation ?code.
          }
        }
      }
  - title: convert mim:RelatierolBron
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatierolbron.
          ?owner sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate sh:class ?class.
          ?predicate rdfs:seeAlso ?relatierolbron
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?owner)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?classcode)) as ?class)
                ?code ?naam ?relatierolbron
        WHERE {
          GRAPH <urn:input> {
            ?relatierolbron a mim:RelatierolBron.
            ?relatierolbron mim:naam ?naam.
            ?relatierolbron skos:notation ?code.
            ?relatiesoort mim:relatierol ?relatierolbron.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?doel skos:notation ?ownercode.
            ?bron skos:notation ?classcode.
          }
        }
      }
  - title: convert mim:RelatierolDoel
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatieroldoel.
          ?owner sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate sh:class ?class.
          ?predicate rdfs:seeAlso ?relatieroldoel.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?owner)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?classcode)) as ?class)
                ?code ?naam ?relatieroldoel
        WHERE {
          GRAPH <urn:input> {
            ?relatieroldoel a mim:RelatierolDoel.
            ?relatieroldoel mim:naam ?naam.
            ?relatieroldoel skos:notation ?code.
            ?relatiesoort mim:relatierol ?relatieroldoel.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?doel skos:notation ?classcode.
            ?bron skos:notation ?ownercode.
          }
        }
      }
  - title: convert mim:RelatierolDoel of mim:Relatieklasse
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?statement rdfs:subClassOf rdf:Statement.
          ?statement sh:property ?subjectps.
          ?statement sh:property ?predicateps.
          ?statement sh:property ?objectps.
          ?predicateps a sh:PropertyShape.
          ?predicateps sh:path rdf:predicate.
          ?predicateps sh:hasValue ?predicate.
          ?predicateps sh:minCount 1 .
          ?predicateps sh:maxCount 1 .
          ?subjectps a sh:PropertyShape.
          ?subjectps sh:path rdf:subject.
          ?subjectps sh:class ?subject.
          ?objectps a sh:PropertyShape.
          ?objectps sh:path rdf:object.
          ?objectps sh:class ?object.
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?statement)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?objectcode)) as ?object)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?subjectcode)) as ?subject)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?predicatecode)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code,".object")) as ?objectps)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code,".subject")) as ?subjectps)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code,".predicate")) as ?predicateps)
                ?code ?naam ?relatieroldoel
        WHERE {
          GRAPH <urn:input> {
            ?relatieroldoel a mim:RelatierolDoel.
            ?relatieroldoel skos:notation ?predicatecode.
            ?relatieklasse a mim:Relatieklasse.
            ?relatieklasse mim:relatierol ?relatieroldoel.
            ?relatieklasse mim:bron ?bron.
            ?relatieklasse mim:doel ?doel.
            ?relatieklasse skos:notation ?code.
            ?doel skos:notation ?objectcode.
            ?bron skos:notation ?subjectcode.
          }
        }
      }
  - title: convert mim:Gegevensgroep
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?attribuutsoort.
          ?ownershape sh:property ?predicate.
          ?predicate a sh:PropertyShape.
          ?predicate sh:name ?code.
          ?predicate sh:path ?property.
          ?predicate sh:nodeKind sh:BlankNode.
          ?predicate sh:class ?class.
          ?predicate rdfs:seeAlso ?gegevensgroep
        }
      }
      WHERE {
        SELECT  (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?property)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode,".",?code)) as ?predicate)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?ownercode)) as ?ownershape)
                (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?typecode)) as ?class)
                ?code ?naam ?gegevensgroep
        WHERE {
          GRAPH <urn:input> {
            ?owner a mim:Objecttype.
            ?owner mim:gegevensgroep ?gegevensgroep.
            ?owner skos:notation ?ownercode.
            ?gegevensgroep a mim:Gegevensgroep.
            ?gegevensgroep mim:naam ?naam.
            ?gegevensgroep mim:gegevensgroeptype ?type.
            ?gegevensgroep skos:notation ?code.
            ?type skos:notation ?typecode.
          }
        }
      }
  - title: convert mim:Referentielijst
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?class a owl:Class, sh:NodeShape.
          ?class sh:targetClass ?class.
          ?class rdfs:label ?naam.
          ?class skos:notation ?code.
          ?class rdfs:seeAlso ?referentielijst.
        }
      }
      WHERE {
        SELECT (iri(concat("http://taxonomie.zorgeloosvastgoed.nl/def/zvg#",?code)) as ?class) ?code ?naam ?referentielijst
        WHERE {
          GRAPH <urn:input> {
            ?referentielijst a mim:Referentielijst.
            ?referentielijst mim:naam ?naam.
            ?referentielijst skos:notation ?code.
          }
        }
      }
  - title: convert mim:Generalisatie
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      INSERT {
        GRAPH <urn:output> {
          ?subclass rdfs:subClassOf ?superclass
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?generalisatie a mim:Generalisatie.
          ?generalisatie mim:subtype ?subtype.
          ?generalisatie mim:supertype ?supertype.
        }
        GRAPH <urn:output> {
          ?subclass rdfs:seeAlso ?subtype.
          ?superclass rdfs:seeAlso ?supertype.
        }
      }
  - title: convert mim:begrip
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX dct: <http://purl.org/dc/terms/>
      INSERT {
        GRAPH <urn:output> {
          ?element dct:subject ?begrip
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:begrip ?begrip
        }
        GRAPH <urn:output> {
          ?element rdfs:seeAlso ?item
        }
      }
  - title: convert mim:definitie
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      INSERT {
        GRAPH <urn:output> {
          ?element rdfs:comment ?definitie
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:definitie ?definitie
          FILTER NOT EXISTS {?item mim:begrip ?begrip}
        }
        GRAPH <urn:output> {
          ?element rdfs:seeAlso ?item
        }
      }
  - title: convert mim:type (mim:PrimitiefDatatype)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:datatype ?datatype
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:PrimitiefDatatype.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?datatype rdfs:seeAlso ?type.
        }
      }
  - title: convert mim:type (mim:Referentielijst)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:class ?refclass
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:Referentielijst.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?refclass rdfs:seeAlso ?type.
        }
      }
  - title: change owl:DatatypeProperty to owl:ObjectProperty for non-primitive datatypes
    query: >
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      DELETE {
        GRAPH <urn:output> {
          ?property a owl:DatatypeProperty
        }
      }
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty
        }
      }
      WHERE {
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape sh:class ?class.
          ?pshape sh:path ?property.
        }
      }
  - title: convert mim:kardinaliteit (minimum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:minCount ?mincount.
        }
      }
      WHERE {
        {
          SELECT ?pshape (strdt(strbefore(?kardinaliteit,".."), xsd:integer) as ?mincount)
          WHERE {
            GRAPH <urn:input> {
              ?element mim:kardinaliteit ?kardinaliteit
            }
            GRAPH <urn:output> {
              ?pshape a sh:PropertyShape.
              ?pshape rdfs:seeAlso ?element
            }
          }
        }
        FILTER (?mincount!="0")
      }
  - title: convert mim:kardinaliteit (maximum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
      		?pshape sh:maxCount ?maxcount.
        }
      }
      WHERE {
        {
          SELECT ?pshape (strdt(strafter(?kardinaliteit,".."), xsd:integer) as ?maxcount)
          WHERE {
            GRAPH <urn:input> {
              ?element mim:kardinaliteit ?kardinaliteit
            }
            GRAPH <urn:output> {
              {
                ?pshape a sh:PropertyShape.
                ?pshape rdfs:seeAlso ?element
              }
              UNION
              {
                ?pshape a sh:PropertyShape.
                ?pshape rdfs:seeAlso ?relatiesoort.
                ?relatiesoort mim:relatierol ?element
              }
            }
          }
        }
        FILTER (?maxcount!="-1")
      }
